apply plugin: 'android-library'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.squareup.dagger:dagger:1.2.1'
    compile 'com.squareup.dagger:dagger-compiler:1.2.1'
    compile 'com.android.support:appcompat-v7:18.0.0'
}

android {
    compileSdkVersion 19
    buildToolsVersion "19"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
    }
}

apply plugin: 'maven'
apply plugin: 'signing'

version = "1.2.0"
group = "com.wmbest.barstool"

def deployRepo = sonatypeRepo

/* Uncomment to deploy locally */
// deployRepo = 'file://localhost/tmp/myRepo'

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: deployRepo) {
            authentication(userName: sonatypeUsername,
                    password: sonatypePassword)
        }

        pom.project {
            artifactId 'barstool'
            name 'Android Debug Toolbar'
            packaging 'aar'
            description 'A Pluggable Debug toolbar for Dagger\'s SET directive'
            url 'https://github.com/wmbest2/Barstool'

            scm {
                url 'scm:git@github.com:wmbest2/Barstool.git'
                connection 'scm:git@github.com:wmbest2/Barstool.git'
                developerConnection 'scm:git@github.com:wmbest2/Barstool.git'
            }

            licenses {
                license {
                    name 'Apache Version 2.0'
                    url 'http://www.apache.org/licenses/'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'wmbest2'
                    name 'William Best'
                    email 'wmbest2@gmail.com'
                }
            }
        }
    }
}
